plugins {
    id 'java-library'
    id 'com.google.protobuf'
    id 'maven-publish'
}

dependencies {
    // Utilisation des variables root pour garantir l'alignement
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" } }
    generateProtoTasks { all()*.plugins { grpc {} } }
}

bootJar { enabled = false }
jar { enabled = true ; archiveClassifier = '' }

java { toolchain { languageVersion = JavaLanguageVersion.of(21) } }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            // FORCE l'écriture des versions résolues dans le POM
            versionMapping {
                usage('java-api') { fromResolutionOf('runtimeClasspath') }
                usage('java-runtime') { fromResolutionOf('runtimeClasspath') }
            }
        }
    }
}

sourceSets {
    main { java { srcDirs 'build/generated/source/proto/main/java', 'build/generated/source/proto/main/grpc' } }
}
tasks.withType(GenerateModuleMetadata) { enabled = false }